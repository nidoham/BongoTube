plugins {
    id 'com.android.application'
    id 'io.gitlab.arturbosch.detekt' version '1.23.4'
}

android {
    namespace 'bd.nidoham.bongo'
    compileSdk 34
    
    defaultConfig {
        applicationId "bd.nidoham.bongo"
        minSdk 26
        targetSdk 33
        versionCode 1
        versionName "1.0"
        
        vectorDrawables { 
            useSupportLibrary true
        }
        
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            if (System.getenv("SIGNING_KEY_ALIAS")) {
                storeFile file('keystore.jks')
                storePassword System.getenv("SIGNING_STORE_PASSWORD")
                keyAlias System.getenv("SIGNING_KEY_ALIAS")
                keyPassword System.getenv("SIGNING_KEY_PASSWORD")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (System.getenv("SIGNING_KEY_ALIAS")) {
                signingConfig signingConfigs.release
            }
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    buildFeatures {
        viewBinding true
        buildConfig true
    }

    lint {
        abortOnError false
        checkReleaseBuilds false
        htmlReport true
        xmlReport true
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    implementation("androidx.constraintlayout:constraintlayout:2.1.4")
    implementation("androidx.appcompat:appcompat:1.6.1")
    implementation("com.google.android.material:material:1.9.0")
    
    // Circular ImageView - Profile pictures এর জন্য
    implementation 'de.hdodenhof:circleimageview:3.1.0'
    
    // androidx Leanback লাইব্রেরি — 
    // Android TV এবং Leanback UI কম্পোনেন্ট ব্যবহারের জন্য প্রয়োজন।
    // এটি TV অ্যাপ্লিকেশন বানানোর জন্য বিভিন্ন UI widget ও helpers দেয়,
    // যেমন BrowseFragment, PlaybackOverlayFragment ইত্যাদি।
    implementation 'androidx.leanback:leanback:1.2.0-alpha04'
    
    // Testing dependencies
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:5.7.0'
    testImplementation 'androidx.test:core:1.5.0'
    testImplementation 'androidx.test.ext:junit:1.1.5'
    testImplementation 'org.robolectric:robolectric:4.11.1'
    
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'
}

detekt {
    toolVersion = "1.23.4"
    config.setFrom(file("$projectDir/config/detekt/detekt.yml"))
    buildUponDefaultConfig = true
}
